plugins {
    id('com.android.application')
    id('kotlin-android')
}

def configValue = { id ->
    return new XmlParser()
            .parse(file("${rootProject.projectDir.path}/app/src/main/res/values/config.xml"))
            .string.find { it.@name.equals id }.text()
}

android {
    namespace 'com.vinaooo.revenger'
    compileSdk 35               // Android 15 - API Level 35
    // buildToolsVersion removido - AGP 8.3.2 usa automaticamente 35.0.0

    defaultConfig {
        applicationId "com.vinaooo.revenger.${configValue('config_id')}"
        minSdk 30                // Android 11 - API Level 30
        targetSdk 35             // Android 15 - API Level 35
        versionCode 1
        versionName '1.0'
        resConfigs 'en'
    }

    signingConfigs {
        debug {
            // mantenha apenas as configurações de keystore padrão
            storeFile file("${System.getProperty("user.home")}/.android/debug.keystore")
            storePassword 'android'
            keyAlias 'AndroidDebugKey'
            keyPassword 'android'
        }
        release {
            storeFile file("${rootProject.projectDir}/revenger.jks")
            storePassword 'ludere'
            keyAlias 'key0'
            keyPassword 'ludere'
        }
    }

    buildTypes {
        debug {
            debuggable = true         // Atributo, não método
            minifyEnabled = false
            shrinkResources = false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled = false
            shrinkResources = false
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable true
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }


    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

task prepareCore {
    doLast {
        for (abi in ['x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a']) {
            def jniAbiFolder = file("${rootProject.projectDir}/app/src/main/jniLibs/$abi")

            if (jniAbiFolder.exists() && jniAbiFolder.list().length > 0)
                continue

            jniAbiFolder.mkdirs()

            download {
                src "https://buildbot.libretro.com/nightly/android/latest/$abi/${configValue('config_core')}_libretro_android.so.zip"
                dest "${rootProject.projectDir}/app/src/main/jniLibs/$abi"
            }
            copy {
                from zipTree("${rootProject.projectDir}/app/src/main/jniLibs/$abi/${configValue('config_core')}_libretro_android.so.zip")
                into "${rootProject.projectDir}/app/src/main/jniLibs/$abi"
                rename "${configValue('config_core')}_libretro_android.so", "libcore.so"
            }
            delete {
                delete "${rootProject.projectDir}/app/src/main/jniLibs/$abi/${configValue('config_core')}_libretro_android.so.zip"
            }
        }
    }
}
preBuild.dependsOn prepareCore

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1-native-mt'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'

    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'

    implementation 'com.github.swordfish90:radialgamepad:0.6.0'
    implementation 'com.github.swordfish90:libretrodroid:0.6.2'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
}
