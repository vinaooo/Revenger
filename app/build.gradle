plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def configValue = { id ->
    return new XmlParser()
            .parse(file("${rootProject.projectDir.path}/app/src/main/res/values/config.xml"))
            .string.find { it.@name.equals id }.text()
}

android {
    namespace = 'com.vinaooo.revenger'
    compileSdk = 36               // Android 16 - API Level 36
    // buildToolsVersion removido - AGP 8.13.0 usa automaticamente 36.0.0

    defaultConfig {
        applicationId "com.vinaooo.revenger.${configValue('config_id')}"
        minSdk 30                // Android 11 - API Level 30
        targetSdk 36             // Android 16 - API Level 36
        versionCode 1
        versionName '1.0'
        resConfigs 'en'
    }

    signingConfigs {
        debug {
            // mantenha apenas as configurações de keystore padrão
            storeFile file("${System.getProperty("user.home")}/.android/debug.keystore")
            storePassword 'android'
            keyAlias 'AndroidDebugKey'
            keyPassword 'android'
        }
        release {
            storeFile file("${rootProject.projectDir}/revenger.jks")
            storePassword 'ludere'
            keyAlias 'key0'
            keyPassword 'ludere'
        }
    }

    buildTypes {
        debug {
            debuggable = true         // Atributo, não método
            minifyEnabled = false
            shrinkResources = false
            signingConfig = signingConfigs.debug
        }
        release {
            minifyEnabled = false
            shrinkResources = false
            signingConfig = signingConfigs.release
        }
    }

    splits {
        abi {
            enable = false
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk = false
        }
    }


    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions {
        jvmTarget = '21'
    }
}

// Simple task compatible with configuration cache
tasks.register('prepareCore') {
    doLast {
        def configCore = configValue('config_core')
        
        ['x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'].each { abi ->
            def jniAbiFolder = file("src/main/jniLibs/$abi")

            if (jniAbiFolder.exists() && jniAbiFolder.list().length > 0) {
                return
            }

            jniAbiFolder.mkdirs()

            def zipFile = file("src/main/jniLibs/$abi/${configCore}_libretro_android.so.zip")

            download {
                src "https://buildbot.libretro.com/nightly/android/latest/$abi/${configCore}_libretro_android.so.zip"
                dest zipFile
            }
            
            project.ant.unzip(src: zipFile, dest: jniAbiFolder) {
                patternset() {
                    include name: "${configCore}_libretro_android.so"
                }
                mapper type: "glob", from: "${configCore}_libretro_android.so", to: "libcore.so"
            }
            
            delete zipFile
        }
    }
}

tasks.named('preBuild') {
    dependsOn('prepareCore')
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.2.20"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'

    implementation 'androidx.activity:activity-ktx:1.11.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.4'

    // RadialGamePad - Updated to 2.0.0 with native Kotlin Flow support
    implementation 'com.github.swordfish90:radialgamepad:2.0.0'
    implementation 'com.github.swordfish90:libretrodroid:0.12.0'

    // RxJava 3 para novos códigos
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.8'
    
    // Phase 9.4: SDK 36 Features - Material Design Components
    implementation 'com.google.android.material:material:1.12.0'
}
